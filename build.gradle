buildscript {
	dependencies {
		classpath 'com.h2database:h2:1.4.197'
		classpath 'mysql:mysql-connector-java:8.0.16'
	}
}

plugins {
    id 'org.springframework.boot' version '2.1.7.RELEASE'
	id "org.flywaydb.flyway" version "6.4.1"
	id 'java'
	id 'eclipse' // optional (to generate Eclipse project files)
	id 'idea' // optional (to generate IntelliJ IDEA project files)
	id 'checkstyle'
	id 'pmd'
	id "de.aaschmid.cpd" version "2.0"
	id 'project-report'
	id 'findbugs'   // Java 9以上だと使えniにあ
	id  'com.github.spotbugs' version '1.7.1'
	id 'jacoco'
	// id "gradle.site" version "0.6"
	// id "com.novoda.static-analysis" version "0.8.1"
	// id "com.monits.staticCodeAnalysis" version "2.6.10"
}
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springBootAdminVersion', '2.1.3')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'de.codecentric:spring-boot-admin-starter-server'
	implementation 'org.apache.commons:commons-lang3:3.9'
	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok:1.18.8'
	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok:1.18.8'

	testCompile 'org.junit.jupiter:junit-jupiter:5.5.1'
	testImplementation ('org.springframework.boot:spring-boot-starter-test'){
		exclude module: 'junit'
	}
	testImplementation 'org.springframework.security:spring-security-test'

}

dependencyManagement {
	imports {
		mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

jacoco {
	toolVersion = "0.8.4"
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled  true
	}
}

checkstyle {
	toolVersion '8.31'
	ignoreFailures = true
	configFile = rootProject.file('config/checkstyle/checkstyle.xml') 
}

tasks.withType(Checkstyle) {
	source = sourceSets.main.allJava
	exclude '**/*Example.java'
}

pmd {
    toolVersion '6.13.0'
	ignoreFailures = true
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/codestyle.xml",
            "category/java/design.xml",
            "category/java/documentation.xml",
            "category/java/errorprone.xml",
            "category/java/multithreading.xml",
            "category/java/performance.xml",
            "category/java/security.xml"
    ]
}

tasks.withType(Pmd) {
	reports {
		xml.enabled true
		html.enabled false
	}
	excludes = [
			"**/*Example.java"
	]
}

cpd {
	toolVersion = '6.13.0'
	ignoreFailures = true
}

cpdCheck {
	reports {
		text.enabled = false
		xml.enabled = true
	}
	source = sourceSets.main.allJava
	exclude '**/*Example.java'
	exclude '**/*Mapper.java'
}

spotbugs {
  toolVersion = '4.0.0-beta1'
}

// To generate an HTML report instead of XML
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports.xml.enabled = false
  reports.html.enabled = true
}

// Java 9 以下
 findbugs {
 	ignoreFailures = true
 }

 tasks.withType(FindBugs) {
     reports {
         xml.enabled = false // これを書くとXML出力を止めることができる
         html.enabled = true
     }
 }
